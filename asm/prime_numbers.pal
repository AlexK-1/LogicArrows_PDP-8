*200

/ Ввод количества простых чисел, которые надо найти
        LAS             / Считывание с КР
        CIA             / Дополнение до 2
        DCA COUNT       / Запись в переменную счётчика

/ Цикл подбора простых чисел
LOOP,   TAD VALUE       / Увеличение числа-кандидата на 2,
        IAC             / чтобы проходиться только по нечётным
        IAC
        DCA VALUE

        TAD FIRST       / Установка указателя в начало списка
        DCA LINK

/ Цикл подбора множителей среди
/ уже найденных простых чисел
FACTOR, CLA CLL
        TAD LINK        / Смещение указателя на 1 вперёд
        IAC
        DCA LINK
        
        TAD I LINK      / Получение множителя
        CIA             / Дополнение до 2 вычитания потом
        DCA MULT        / Запись в переменную множителя

        TAD VALUE       / Получение кандидата
        RAR             / Деление на 2 путём сдвига вправо
        TAD MULT        / Вычитание множителя

        SPA             / Если множитель больше половины
        JMP PRIME       /   кандидата, то число простое 
                        /   и переходим к его обработке
        CLA CLL         / Возвращаем число-кандидат
        TAD VALUE

/ Цикл вычитания множителя
SUB,    TAD MULT
        SNA             / Если 0, число не простое
        JMP LOOP
        SMA             / Если >0, продолжаем вычитать
        JMP SUB
        JMP FACTOR      / Если <0, берём следующий множитель

/ Обработка найденного простого числа
PRIME,  CLA CLL
        TAD LAST        / Смещаем указатель на последнее число
        IAC
        DCA LAST
        
        / Вывод простого числа на циферблат
        TAD VALUE
        DIALL           / Вывод младших 8 бит
        RTL             / Сдвиг Старших 4 бит
        RTL
        RAL
        AND F           / Обрезание лишних битов
        DIALH           / Вывод старших 4 бит
        CLA CLL

        TAD VALUE       / Сохраняем найденное
        DCA I LAST      / простое число в список

        ISZ COUNT       / Определение конца цикла
        JMP LOOP

        TAD VALUE       / Вывод последнего просто числа в НР
        HLT             / Остановка

/ Переменные
COUNT,  0000            / Счётчик для цикла
VALUE,  0003            / Число-кандидат
MULT,   0000            / Множитель
LINK,   0000            / Указатель на последнее простое число
FIRST,  NLIST-1         / Ссылка на первое число в списке
LAST,   NLIST           / Ссылка на последнее найденное
                        /   в списке

/ Константы
F,      0017    / Младшие 4 бита единицы, остальные - нули

/Команды
DIALL=  6064    / Вывод в младший байт циферблата
DIALH=  6066    / Вывод в старший байт циферблата

/ Список с найденными простыми числами
NLIST,  0003

$
