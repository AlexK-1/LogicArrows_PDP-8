*200

/ Ввод первого числа
FNUM,	KSF             / Инпут с клавиатуры
         JMP .-1
        KRB
	TPC		/ Вывод в терминал

	TAD NENTER	/ Получаем введённую цифру и переходим к обработке действия, если введена не цифра
	SPA
	JMP ACTION
	DCA FIGURE
	TAD FIGURE
	TAD NEGT1
	SNA
	JMP ACTION
	CLA CLL

	TAD FNUMD	/ Читаем число
	RTL		/ Умножение на 10
	TAD FNUMD
	RAL
	TAD FIGURE	/ Добавляем считанную цифру

	DCA FNUMD	/ Сохраняем результат

	JMP FNUM

/ Обработка ввода действия
ACTION,	DCA ACTS

/ Ввод второго числа
SNUM,	KSF             / Инпут с клавиатуры
        JMP .-1
        KRB
	TPC		/ Вывод в терминал

	TAD NEQUAL	/ Если введено =, остановка ввода
	SNA
	JMP CALC

	TAD NBITEQ 	/ Получаем введённую цифру и сохраняем
	DCA FIGURE
	CLL

	TAD SNUMD	/ Читаем число
	RTL		/ Умножение на 10
	TAD SNUMD
	RAL
	TAD FIGURE	/ Добавляем считанную цифру

	DCA SNUMD	/ Сохраняем результат

	JMP SNUM

/ Переход на нужную операцию
CALC,	TAD ACTS
        IAC             / Если /
        SNA
        JMP DIVIDE
	TAD DTPLUS	/ Если +
	SNA
	JMP PLUS
	TAD DTMIN	/ Если -
	SNA
	JMP MINUS
        TAD DTMULT	/ Если *
	SNA
	JMP MULT

	HLT

/ Операция +
PLUS,	TAD FNUMD
	TAD SNUMD

	JMP OUT

/ Операция -
MINUS,	TAD SNUMD
	CIA
	TAD FNUMD

	JMP OUT

/ Операция *
MULT,   TAD SNUMD
        DCA MULTPR
        TAD FNUMD
        MQL MUY
MULTPR, 0000
        MQA

        JMP OUT

/ Операция /
DIVIDE, TAD SNUMD
        DCA DIVISR
        TAD FNUMD
        MQL DVI
DIVISR, 0000
        CLA MQA

        JMP OUT

/ Вывод
OUT,	DCA RESULT	/ Сохранение результата

        / Вывод тысяч
	TAD POST3       / Сохранение 1000 в делителе
        DCA T3DVSR
        TAD RESULT
        MQL DVI         / Деление результата на 1000
T3DVSR, 0000
        DCA RESULT      / Сохранение остатка
        CLA MQA         / Получение частного
        TAD DIGGEN      / Создание кода символа частного
        TPC             / Вывод цифры в терминал
        CLA

        / Вывод сотен
	TAD POST2       / Сохранение 100 в делителе
        DCA T2DVSR
        TAD RESULT
        MQL DVI         / Деление результата на 100
T2DVSR, 0000
        DCA RESULT      / Сохранение остатка
        CLA MQA         / Получение частного
        TAD DIGGEN      / Создание кода символа частного
        TPC             / Вывод цифры в терминал
        CLA

        / Вывод десятков
	TAD POST1       / Сохранение 10 в делителе
        DCA T1DVSR
        TAD RESULT
        MQL DVI         / Деление результата на 10
T1DVSR, 0000
        DCA RESULT      / Сохранение остатка
        CLA MQA         / Получение частного
        TAD DIGGEN      / Создание кода символа частного
        TPC             / Вывод цифры в терминал
        CLA

        / Вывод единиц
        TAD RESULT
	TAD DIGGEN
	TPC

	HLT

/ Переменные
FIGURE,	0000		/ Введённая/выведенная цифра
FNUMD,	0000		/ Первое число
SNUMD,	0000		/ Первое число
ACTS,	0000		/ Символ действия, которое надо выполнить
RESULT,	0000		/ Результат вычисления

/ Константы
NENTER,	7720
NEGT1,	7766		/ -10
POST3,	1750		/ 1000
POST2,	0144		/ 100
POST1,	0012		/ 10
NEQUAL,	7703		/ -=
NBITEQ,	0015		/ Для определения числа после клавиши =
DTPLUS,	0004		/ Слагаемое для определения клавиши +
DTMIN,	7776		/ Слагаемое для определения клавиши -
DTMULT,	0003		/ Слагаемое для определения клавиши *
DIGGEN,	0060		/ Слагаемое для получения символа цифры

/ Команды
MUY = 7405
MQA = 7501
MQL = 7421
DVI = 7407